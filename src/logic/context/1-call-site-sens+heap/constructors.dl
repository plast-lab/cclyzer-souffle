//------------------------------------------------------------------------------
// [1 Call-site Sensitive + Heap Analysis]
//
// In this analysis, context and alocation context is in both cases
// the latest invocation instruction.
//------------------------------------------------------------------------------

.type ContextItem = symbol | Instruction
.type Context = [ci: ContextItem]
.type AContext = [ci :ContextItem]


.decl context(?ctx:Context)
.decl acontext(?allocCtx:AContext)
.decl context_item(?item:ContextItem)

//-----------------------------------------------------
// Context Constructors
//-----------------------------------------------------

//.decl context_new(?item:ContextItem, ?ctx:Context)
.decl acontext_new(?item:ContextItem, ?actx:AContext)


.decl context_request(?callerCtx: Context, ?actx: AContext, ?invoc: Instruction, ?tofunction: Function)
.decl context_response(?callerCtx: Context, ?actx: AContext, ?invoc: Instruction, ?ctx: Context)

context_response(?callerCtx, ?actx, ?invoc, ?ctx) :-
   context_request(?callerCtx, ?actx, ?invoc, _),
   ?ctx = [?invoc].


.decl acontext_request(?invoc: ContextItem)
.decl acontext_response(?invoc: ContextItem, ?ctx: AContext)

acontext_response(?invoc, ?actx) :-
   acontext_request(?invoc),
   ?actx = [?invoc].


//-----------------------------------------------------
// Context Item Constructors
//-----------------------------------------------------

.decl context_item_by_invoc(?invoc:Instruction, ?ctxItem:ContextItem)
.decl context_item_invoc(?ctxItem:ContextItem, ?invoc:Instruction)


context_item_invoc(?ctxItem, ?invoc) :-
   context_item_by_invoc(?invoc, ?ctxItem).

context_item(?instr1)
 , context_item_by_invoc(?instr1, ?instr2)
 :-
   call_instruction(?instr1),
   ?instr1= ?instr2.

context_item(?instr1)
 , context_item_by_invoc(?instr1, ?instr2)
 :-
   invoke_instruction(?instr1),
   ?instr1=?instr2.


// Dummy context items (needed for empty contexts)
.decl matches_string_context(?str:symbol)

matches_string_context(?str):-
   ?str = "<<empty-context>>".
matches_string_context(?str):-
   ?str = "<<main-context>>".
matches_string_context(?str):-
   ?str = "<<startup-context>>".
matches_string_context(?str):-
   ?str = "<<exit-context>>".

.decl dummy_context_item_by_str(?str:symbol,?ctxItem:ContextItem)
context_item(?ctxItem)
 , dummy_context_item_by_str(?str,?ctxItem)
 :-
   matches_string_context(?str),
   ?ctxItem=?str.

//-----------------------------------------------------
// Macro-constructors
//-----------------------------------------------------
.decl record(?ctx: Context, ?alloc: Allocation, ?actx: AContext)
// record[Ctx, Alloc] = ACtx ->
//    context(Ctx), allocation(Alloc), acontext(ACtx).
.decl merge(?callerCtx: Context, ?invoc: Instruction, ?actx: AContext, ?alloc: Allocation, ?calleeCtx: Context)
// merge[CallerCtx, Invoc, ACtx, Alloc] = CalleeCtx ->
//    context(CallerCtx), context(CalleeCtx),
//    instruction(Invoc), acontext(ACtx), allocation(Alloc).

// // Macro-definitions
/*
acontext_new(?item, ?actx) :-
   record(?ctx, _, ?actx),
   context_new(?item, ?ctx).


context_new(?newItem, ?calleeCtx) :-
   merge(?callerCtx, ?invoc, _, _, ?calleeCtx),
   context_new(_,?callerCtx),
   context_item_by_invoc(?invoc, ?newItem).
*/
//-----------------------------------------------------
// Unpacking context items
//-----------------------------------------------------

.decl context_getitem(?ctx:Context, ?item:ContextItem)
context_getitem(?ctx, ?item):-
   context_response(_,_,?item, ?ctx).

.decl acontext_getitem(?ctx:AContext, ?item:ContextItem)
acontext_getitem(?ctx, ?item):-
   acontext_new(?item, ?ctx).

.decl context_getinvoc(?ctx:Context, ?invoc:Instruction)
context_getinvoc(?ctx, ?invoc):-
   context_getitem(?ctx, ?item),
   context_item_invoc(?item, ?invoc).

.decl acontext_getinvoc(?ctx:AContext, ?invoc:Instruction)
acontext_getinvoc(?ctx, ?invoc) :-
   acontext_getitem(?ctx, ?item),
   context_item_invoc(?item, ?invoc).


//Already generated output relations of 2-call-sensitivity
.decl context_getinvoc0(?dummy : number)
.decl context_getitem0(?dummy : number)
.decl context_getinvoc1(?dummy : number)
.decl context_getitem1(?dummy : number)
