/// 1. Alloca Instructions

.decl var_points_to6(ACtx: AContext, TypedAlloc: symbol, Ctx: Context, Var: symbol)
.decl var_points_to7(ACtx: AContext, TypedAlloc: symbol, Ctx: Context, Var: symbol)
.decl var_points_to8(ACtx: AContext, TypedAlloc: symbol, Ctx: Context, Var: symbol)

.output var_points_to6
.output var_points_to7
.output var_points_to8


var_points_to6(?actx, ?stackAlloc, ?ctx, ?var),
acontext(?actx),
stack_allocation(?stackAlloc),
stack_allocation_by_instruction(?instr, ?stackAlloc),
var_points_to(?actx, ?stackAlloc, ?ctx, ?var)
 :-
   alloca_instruction(?instr),
   instruction_assigns_to(?instr, ?var),
   instruction_in_function(?instr, ?instrFunc),
   function_has_name(?instrFunc, ?funcName),
   variable_has_type(?var, ?type),
   variable_has_name(?var, ?name),
   ?stackAlloc = cat(cat(cat(cat(cat(cat("*stack_alloc", ?funcName),"["),?type)," "),?name),"]"),
   reachable_context(?ctx, ?instrFunc),
   // Extra
   AContextFromContext(?ctx, ?actx).


allocation_type(?stackAlloc, ?type) :-
   alloca_instruction(?instr),
   alloca_instruction_type(?instr,?type),
   stack_allocation_by_instruction(?instr, ?stackAlloc).

/// 2. Parameters with pass-by-value semantics

var_points_to7(?actx, ?stackAlloc, ?ctx, ?param),
acontext(?actx),
stack_allocation(?stackAlloc),
stack_allocation_by_parameter(?func, ?index, ?stackAlloc),
var_points_to(?actx, ?stackAlloc, ?ctx, ?param)
 :-
   function_param_by_value(?func, ?index),
   function_param(?func, ?index, ?param),
   function_has_name(?func, ?funcName),
   variable_has_type(?param,?type),
   variable_has_name(?param,?name),
   ?stackAlloc = cat(cat(cat(cat(cat(cat("*stack_alloc",?funcName),"["),?type)," "),?name),"]"),

   reachable_context(?ctx, ?func),
//   Extra
   AContextFromContext(?ctx, ?actx).



allocation_type(?stackAlloc, ?type) :-
   stack_allocation_by_parameter(?func, ?index, ?stackAlloc),
   function_has_type(?func,?funcType),
   function_type_param(?funcType, ?index, ?paramType),
   pointer_type_has_component(?paramType, ?type).





//---------------------------------------------------------------------
// [Heap Allocations]
//
// In contrast to the other types of allocations, an allocation on the
// heap is not always associated with a type (at least locally).
//---------------------------------------------------------------------


var_points_to8(?actx, ?heapAlloc, ?ctx, ?var),
acontext(?actx),
var_points_to(?actx, ?heapAlloc, ?ctx, ?var),
heap_allocation(?heapAlloc),
heap_allocation_by_instruction(?instr, ?heapAlloc) :-
   heap_alloc_function(?func),
   ( call_instruction_fn_target(?instr, ?func)
   ; invoke_instruction_fn_target(?instr, ?func)),
   instruction_assigns_to(?instr, ?var),
   instruction_in_function(?instr,  ?instrFunc),
   function_has_name(?instrFunc, ?funcName),

   variable_has_type(?var,?type),
   variable_has_name(?var,?name),

   ?heapAlloc = cat(cat(cat(cat(cat(cat("*heap_alloc", ?funcName),"["),?type)," "),?name),"]"),
   reachable_context(?ctx, ?instrFunc),
   // Extra
   AContextFromContext(?ctx, ?actx).

heap_alloc_function(?func) :-
   function_has_name(?func, "@malloc")
 ; function_has_name(?func, "@calloc")
 ; function_has_name(?func, "@realloc").

heap_allocation(?heapAlloc),
heap_allocation_by_malloc(?insn, ?heapAlloc) :-
  function_has_name(?func, "@malloc"),
  call_instruction_fn_target(?insn, ?func),
  heap_allocation_by_instruction(?insn, ?heapAlloc).

heap_allocation(?heapAlloc),
heap_allocation_by_calloc(?insn, ?heapAlloc) :-
  function_has_name(?func, "@calloc"),
  call_instruction_fn_target(?insn, ?func),
  heap_allocation_by_instruction(?insn, ?heapAlloc).

heap_allocation(?heapAlloc),
heap_allocation_by_realloc(?insn, ?heapAlloc) :-
  function_has_name(?func, "@realloc"),
  call_instruction_fn_target(?insn, ?func),
  heap_allocation_by_instruction(?insn, ?heapAlloc).



// !! Track heap allocation types !! Note that a single heap
// allocation is just a series of bytes, which can flow to multiple
// variables of different types in principle.
//
// Consider a generic wrapper to @malloc (that calls @malloc inside
// its body). This allocation inside the wrapper function will flow to
// all sorts of places that have nothing to do with each other, and
// will probably involve many heterogeneous types.

.decl heap_allocation_types(?type: Type, ?actx: AContext, ?heapAlloc: HeapAllocation)
heap_allocation_types(?type, ?actx, ?heapAlloc) :-
   heap_allocation(?heapAlloc),
   operand_points_to(?actx, ?heapAlloc, _, ?var),
   operand_has_type(?var, ?ptrType),
   pointer_type_has_component(?ptrType, ?type).


//---------------------------------------------------------------------
// [Global Allocations]
//
// Note that the type of a global allocation is readily available at
// any case.
//---------------------------------------------------------------------


/// 1. Global variable allocations

global_allocation(?alloc),
global_allocation_by_variable(?gVar, ?alloc):-
   global_variable(?gVar),

   global_variable_has_name(?gVar, ?name),
   ?alloc =  cat("*global_alloc",?name).

allocation_type(?alloc, ?type):-
   global_allocation_by_variable(?gVar, ?alloc),
   global_variable_has_type(?gVar, ?type).

/// 2. Function Allocations

global_allocation(?alloc),
global_allocation_by_function(?func, ?alloc):-
   (function(?func);
   function_decl(?func),
   !function_decl_to_defn(?func,_)),
   
   function_has_name(?func, ?name),
   ?alloc = cat("*global_alloc",?name).



allocation_type(?alloc, ?type):-
   global_allocation_by_function(?func, ?alloc),
   function_has_type(?func,?type).

//----------------------------------
// [Additional pseudo-allocations]
//----------------------------------


// An unknown location points to an unknown location

ptr_points_to(?actx, ?unknown, ?actx, ?unknown) :-
   empty_acontext(?actx),
   unknown_location(?unknown).
/*
//------------------------------------------------------------------------------
// [Sanity checks]
//
// Only empty contexts are associated with global and pseudo-allocs.
//------------------------------------------------------------------------------

_allocation_with_context(ACtx, Alloc) <-
   var_points_to(ACtx, Alloc, _, _).

_allocation_with_context(ACtx, Alloc) <-
   ptr_points_to(ACtx, Alloc, _, _).

_allocation_with_context(ACtx, Alloc) <-
   ptr_points_to(_, _, ACtx, Alloc).

unknown_location[] = Unknown
 , empty_acontext[] = EmptyContext
 , _allocation_with_context(ACtx, Unknown)
 ->
   ACtx = EmptyContext.

null_location[] = Null
 , empty_acontext[] = EmptyContext
 , _allocation_with_context(ACtx, Null)
 ->
   ACtx = EmptyContext.

global_allocation(Alloc)
 , empty_acontext[] = EmptyContext
 , _allocation_with_context(ACtx, Alloc)
 ->
   ACtx = EmptyContext.
*/


//------------------------------------------------
// [Allocation String Representation Protocol]
//------------------------------------------------

// Every allocation must have a string representation
//.decl allocation_to_string(?alloc:Allocation , ?str: symbol)

// Human readable representation for stack allocations

allocation_to_string(?alloc, ?str) :-
   stack_allocation_by_instruction(_, ?alloc),
   ?str = ?alloc.

allocation_to_string(?alloc, ?str) :-
   stack_allocation_by_parameter(_, _, ?alloc),
   ?str = ?alloc.

// Human readable representation for heap allocations

allocation_to_string(?alloc, ?str) :-
   heap_allocation_by_instruction(_, ?alloc),
   ?str = ?alloc.

// Human readable representation for global variable allocations

allocation_to_string(?alloc, ?str) :-
   global_allocation_by_variable(_, ?alloc),
   ?str = ?alloc.

// Human readable representation for function allocations

allocation_to_string(?alloc, ?str) :-
   global_allocation_by_function(_, ?alloc),
   ?str = ?alloc.

//------------------------------------------------
// [Allocation Position Info]
//------------------------------------------------

allocation_pos(?alloc, ?line, ?column) :-
   (stack_allocation_by_instruction(?insn, ?alloc);
   heap_allocation_by_instruction(?insn, ?alloc)),
   instruction_pos(?insn, ?line, ?column).

allocation_pos(?alloc, ?line, ?column) :-
   stack_allocation_by_parameter(?func, ?index, ?alloc),
   function_param(?func, ?index, ?param),
   variable_has_debug_decl_pos(?param, ?line, ?column).

